plugins {
  id 'java'
}

group 'org.apache.samza'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
  maven { url "https://repository.apache.org/content/groups/public" }
}

ext {
  samza_version="1.2.0"
  kafka_version="0.11.0.2"
  hadoop_version="2.6.1"
  slf4j_version ="1.7.7"
}

configurations {
  shell
}

task distTar(dependsOn: 'build', type: Tar) {
  destinationDir(new File(project.buildDir, "/distributions"))
  compression(Compression.GZIP)
  classifier('dist')
  extension('tar.gz')
  into("config") {
    from("src/main/config") {
      include "config.properties"
      // expand the Maven tokens with Gradle equivalents.
      filter { String line ->
        line.replaceAll('[\$][{]basedir[}]', project.projectDir.toString()).replaceAll('[\$][{]project.artifactId[}]', project.name.toString()).replaceAll('/target/', '/build/distributions/').replaceAll('[\$][{]pom.version[}]', version)
      }
    }
  }

  into("bin") {
    from {
      configurations.shell.collect { tarTree(it) }
    }
  }

  into("lib") {
    from configurations.runtime
    from configurations.runtime.artifacts.files
    from("src/main/resources/") {
      include "log4j*.xml"
    }
  }
}

task copyTar(dependsOn: distTar, type: Copy) {
  from "build/distributions"
  into "${rootProject.rootDir}/dist"
  include "*.tar.gz"
}

build.finalizedBy(distTar)
distTar.finalizedBy(copyTar)

dependencies {

  compile("org.apache.samza:samza-api:$samza_version")
  compile("org.apache.samza:samza-kv_2.11:$samza_version")
  compile("org.apache.samza:samza-kafka_2.11:$samza_version")
  compile("org.apache.samza:samza-kv-rocksdb_2.11:$samza_version")
  compile("org.apache.samza:samza-kv-rocksdb_2.11:$samza_version")

  compile("org.slf4j:slf4j-api:$slf4j_version")
  compile("org.slf4j:slf4j-log4j12:$slf4j_version")

  shell (group: 'org.apache.samza', name: 'samza-shell',  ext: 'tgz', classifier: 'dist', version: "$samza_version")

  testCompile(group: 'junit', name: 'junit', version: "4.12")

  runtime("org.apache.samza:samza-core_2.11:$samza_version")
  runtime("org.apache.samza:samza-log4j_2.11:$samza_version")
  runtime("org.apache.samza:samza-shell:$samza_version")
  runtime("org.apache.samza:samza-yarn_2.11:$samza_version")
  runtime("org.apache.kafka:kafka_2.11:$kafka_version")
  runtime("org.apache.hadoop:hadoop-hdfs:$hadoop_version")
}