plugins {
  id 'java'
  id 'distribution'
}

group 'org.apache.samza'
version '0.0.1'

sourceCompatibility = 1.8

repositories {
  mavenCentral()
}

ext {
  samza_version = "1.2.0"
  hadoop_version = "2.6.1"
}

configurations {
  binary
}

distributions {
  main {
    baseName = project.name
    contents {
      into('lib/') {
        from jar
        from configurations.runtimeClasspath
        dirMode = 0755
        fileMode = 0644
      }
      into('bin/') {
        from({ tarTree(configurations.binary.singleFile) }) {
          include '*'
        }
      }
      into('config/') {
        from('src/main/config') {
          include '*.properties'
          dirMode = 0755
          fileMode = 0644
        }
      }
    }
  }
}

distTar {
  compression = Compression.GZIP
}

dependencies {
  compile(group: "org.apache.samza", name: "samza-api", version: samza_version)
  compile(group: "org.apache.samza", name: "samza-core_2.12", version: samza_version)
  compile(group: "org.apache.samza", name: "samza-kv-rocksdb_2.12", version: samza_version)
  compile(group: "org.apache.samza", name: "samza-kafka_2.12", version: samza_version)
  binary("org.apache.samza:samza-shell:$samza_version:dist@tgz")
  testCompile group: 'junit', name: 'junit', version: '4.12'
}